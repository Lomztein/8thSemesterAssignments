grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

MathExp:
	(assignments += Assignment)+
;

Assignment:
	'var' name=ID '=' exp=Exp
;

Exp: PlusOrMinus;

PlusOrMinus returns Exp:
	MultOrDiv (
		({Plus.left=current} '+' | {Minus.left=current} '-')
		right=MultOrDiv
	)*
;

MultOrDiv returns Exp:
	Primary (
		({Mult.left=current} '*' | {Div.left=current} '/')
		right=Primary
	)*
;

Primary returns Exp:
	Litteral | Parenthesis | VariableUse | LetEnd
;

Litteral:
	value=INT
;

Parenthesis:
	'(' exp=Exp ')'
;

VariableUse:
	ref = [Assignment]
;

LetEnd:
	'let' name=ID '=' exp=Exp 'in' in=Exp 'end'
;